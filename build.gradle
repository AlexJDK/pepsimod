/*
 * Adapted from the Wizardry License
 *
 * Copyright (c) 2017-2019 DaPorkchop_
 *
 * Permission is hereby granted to any persons and/or organizations using this software to copy, modify, merge, publish, and distribute it.
 * Said persons and/or organizations are not allowed to use the software or any derivatives of the work for commercial use or any other means to generate income, nor are they allowed to claim this software as their own.
 *
 * The persons and/or organizations are also disallowed from sub-licensing and/or trademarking this software without explicit permission from DaPorkchop_.
 *
 * Any persons and/or organizations using this software must disclose their source code and have it publicly available, include this license, provide sufficient credit to the original author of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NON INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

buildscript {
    repositories {
        jcenter()
        maven {
            name = "MinecraftForge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "SpongePowered"
            url = "http://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.4-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"

ext {
    //general things
    javaVersion = "1.8"
    pepsimodVersion = "1.11"

    //minecraft things
    mcpmappingsVersion = "stable_39"
    minecraftforgeVersion = "14.23.5.2836"
    minecraftVersion = "1.12.2"

    //dependency things
    guavaVersion = "21.0"
    mixinVersion = "0.7.11-SNAPSHOT"
    porklibVersion = "0.3.0-SNAPSHOT"
}

group "net.daporkchop"
version "v$pepsimodVersion-$minecraftVersion"

archivesBaseName = "pepsimod"
sourceCompatibility = targetCompatibility = "$javaVersion"

configurations.all {
    resolutionStrategy {
        force("com.google.guava:guava:$guavaVersion")
    }
}

minecraft {
    version = "$minecraftVersion-$minecraftforgeVersion"
    runDir = "run"
    mappings = "$mcpmappingsVersion"
    makeObfSourceJar = false
    coreMod = "net.daporkchop.pepsimod.PepsimodMixinLoader"
}

repositories {
    mavenLocal()
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven {
        name = "spongepowered-repo"
        url = "http://repo.spongepowered.org/maven/"
    }
    mavenCentral()
}

dependencies {
    compile("org.spongepowered:mixin:$mixinVersion") {
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "gson"
        exclude module: "commons-io"
        exclude module: "log4j-core"
    }

    compile "net.daporkchop.lib:common:$porklibVersion"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    rename '(.+_at.cfg)', 'META-INF/$1' //i hate gradle for not letting me use double quotes here
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, "mixins.pepsimod.refmap.json"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
        classpath = sourceSets.main.compileClasspath
    }
}

shadowJar {
    dependencies {
        dependency("org.spongepowered:mixin")

        dependency("net.daporkchop.lib:unsafe")
        dependency("net.daporkchop.lib:common")
    }
    exclude "dummyThing"
    exclude "LICENSE.txt"
    classifier = null
}

build.dependsOn(shadowJar)

jar {
    manifest {
        attributes(
                "MixinConfigs": "mixins.pepsimod.json",
                "tweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0,
                "FMLCorePluginContainsFMLMod": "true",
                "FMLCorePlugin": "net.daporkchop.pepsimod.PepsimodMixinLoader",
                "ForceLoadAsMod": "true"
        )
    }
}
