/*
 * Adapted from the Wizardry License
 *
 * Copyright (c) 2017-2019 DaPorkchop_
 *
 * Permission is hereby granted to any persons and/or organizations using this software to copy, modify, merge, publish, and distribute it.
 * Said persons and/or organizations are not allowed to use the software or any derivatives of the work for commercial use or any other means to generate income, nor are they allowed to claim this software as their own.
 *
 * The persons and/or organizations are also disallowed from sub-licensing and/or trademarking this software without explicit permission from DaPorkchop_.
 *
 * Any persons and/or organizations using this software must disclose their source code and have it publicly available, include this license, provide sufficient credit to the original author of the project (IE: DaPorkchop_), as well as provide a link to the original project.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NON INFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

import java.util.concurrent.atomic.AtomicReference

buildscript {
    repositories {
        jcenter()
        maven {
            name = "MinecraftForge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "SpongePowered"
            url = "http://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

plugins {
    id "io.franzbecker.gradle-lombok" version "1.14"
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

ext {
    //general things
    javaVersion = "1.8"
    pepsimodVersion = "12.0"

    //minecraft things
    mcpmappingsVersion = "stable_39"
    minecraftforgeVersion = "14.23.5.2836"
    minecraftVersion = "1.12.2"

    //dependency things
    guavaVersion = "21.0"
    mixinVersion = "0.7.11-SNAPSHOT"
    porklibVersion = "0.4.0-SNAPSHOT"
}

group "net.daporkchop"
version "$pepsimodVersion"

sourceCompatibility = targetCompatibility = "$javaVersion"

configurations {
    shade
    all {
        resolutionStrategy {
            force("com.google.guava:guava:$guavaVersion")
        }
    }
}

minecraft {
    version = "$minecraftVersion-$minecraftforgeVersion"
    runDir = "run"
    mappings = "$mcpmappingsVersion"
    makeObfSourceJar = false
}

repositories {
    mavenLocal()
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven {
        name = "spongepowered-repo"
        url = "http://repo.spongepowered.org/maven/"
    }
    mavenCentral()
}

dependencies {
    shade("org.spongepowered:mixin:$mixinVersion") {
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "gson"
        exclude module: "commons-io"
        exclude module: "log4j-core"
    }

    shade "net.daporkchop.lib:common:$porklibVersion"

    compile configurations.shade.dependencies
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": "$pepsimodVersion-$minecraftVersion", "mcversion": "$minecraftVersion"
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    rename '(.+_at.cfg)', 'META-INF/$1' //i hate gradle for not letting me use double quotes here
}

mixin {
    defaultObfuscationEnv srg
}

jar {
    archiveName "pepsimod-v$pepsimodVersion-$minecraftVersion" + ".jar"

    from {
        configurations.shade.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "LICENSE.txt"
        exclude "dummyThing"
    }
    manifest {
        attributes(
                "MixinConfigs": "mixins.pepsimod.json",
                "tweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0,
                "FMLCorePluginContainsFMLMod": "true",
                "FMLCorePlugin": "net.daporkchop.pepsimod.PepsimodMixinLoader",
                "ForceLoadAsMod": "true",
                "FMLAT": "pepsimod_at.cfg"
        )
    }
}

//sets JVM args for genIntellijRuns
AtomicReference<String> mixinPath = new AtomicReference<>(null);
configurations.compile.each {
    String absolutePath = it.absolutePath
    if(absolutePath.contains("mixin")) {
        mixinPath.set(absolutePath)
    }
}
if (mixinPath != null)  {
    minecraft.clientJvmArgs = minecraft.serverJvmArgs = Arrays.asList(
            "-Dfml.coreMods.load=net.daporkchop.pepsimod.PepsimodMixinLoader",
            "-Dmixin.checks.interfaces=true",
            "-Dmixin.debug.export=true",
            "-Dmixin.debug.verbose=true",
            "-Dmixin.hotSwap=true",
            "-javaagent:" + mixinPath,
            "-Xmx2G",
            "-XX:+UseG1GC",
            "-ea",
            "-da:io.netty..."
    )
} else {
    throw new IllegalStateException("Unable to find mixin path!")
}
